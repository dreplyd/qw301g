#include <stack>
#include <Wire.h>
#include <WiFi.h>
#include <WiFiClientSecure.h>
#include <HTTPClient.h>
#include <UniversalTelegramBot.h>
#include <Adafruit_HTU21DF.h>
#include <ArduinoJson.h>


const char* WIFI_SSID = "";
const char* WIFI_PASSWORD = "";
const char* BOT_TOKEN = "";

WiFiClientSecure client;
UniversalTelegramBot bot(BOT_TOKEN, client);
Adafruit_HTU21DF htu = Adafruit_HTU21DF();

unsigned long last_time_bot_accessed = 0;
unsigned long previous_millis = 0;
unsigned int readings_count = 0;
const unsigned long bot_request_delay = 1000;
const unsigned long request_time_interval = 5 * 60 * 1000;
const unsigned long readings_interval = 1 * 60 * 1000;
const int max_readings_count = request_time_interval / readings_interval;
const char* main_menu_keyboard = "[[\"–ü–æ—Ç–æ—á–Ω—ñ –ø–æ–∫–∞–∑–Ω–∏–∫–∏\", \"–ó–≤—ñ—Ç –∑–∞ —Ç–∏–∂–¥–µ–Ω—å\"]]";
const char* report_menu_keyboard = "[[\"–¢–µ–º–ø–µ—Ä–∞—Ç—É—Ä–∞\", \"–í–æ–ª–æ–≥—ñ—Å—Ç—å\"], [\"–ù–∞–∑–∞–¥\"]]";
String server_api = "";

enum MenuState {
  MAIN_MENU,
  REPORT_MENU,
};
std::stack<MenuState> menu_state;

struct SensorData {
  float temperature;
  float humidity;
  uint32_t timestamp;
};
SensorData sensor_data[max_readings_count];


uint32_t getCurrentTime() {
  time_t now = time(nullptr);

  return static_cast<uint32_t>(now);
}


String getKeyboardByMenu() {
  switch (menu_state.top()) {
    case MAIN_MENU:
      return main_menu_keyboard;
    case REPORT_MENU:
      return report_menu_keyboard;
  }
}


void sendDataToServer() {
  HTTPClient http;
  http.begin(server_api + "/save-data");
  http.addHeader("Content-Type", "application/json");

  const size_t capacity = JSON_ARRAY_SIZE(max_readings_count) + max_readings_count * JSON_OBJECT_SIZE(3);
  StaticJsonDocument<capacity> doc;

  JsonArray json_array = doc.to<JsonArray>();
  for (int i = 0; i < max_readings_count; i++) {
    JsonObject json_object = json_array.createNestedObject();
    json_object["temperature"] = sensor_data[i].temperature;
    json_object["humidity"] = sensor_data[i].humidity;
    json_object["timestamp"] = sensor_data[i].timestamp;
  }

  String json_string;
  serializeJson(doc, json_string);

  int http_status_code = http.POST(json_string);

  if (http_status_code == HTTP_CODE_OK) {
    readings_count = 0;
  }

  http.end();
}


void getPlotByType(String chat_id, String type) {
  String reply_text;

  HTTPClient http;
  http.begin(server_api + "/generate-plot/" + type + "/" + chat_id);

  int http_status_code = http.GET();

  if (http_status_code == HTTP_CODE_OK) {
    String response = http.getString();

    StaticJsonDocument<64> doc;
    deserializeJson(doc, response);

    const char* plot_name = doc["plot"];
    String plot_url = server_api + "/static/" + plot_name + "?timestatmp=" + String(millis());

    reply_text = "–û—Å—å —Å—Ç–≤–æ—Ä–µ–Ω–∏–π –≥—Ä–∞—Ñ—ñ–∫ –Ω–∞ –æ—Å–Ω–æ–≤—ñ –¥–∞–Ω–∏—Ö –∑–∞ –æ—Å—Ç–∞–Ω–Ω—ñ–π —Ç–∏–∂–¥–µ–Ω—å üìà:\n";
    bot.sendMessage(chat_id, reply_text);
    bot.sendPhoto(chat_id, plot_url);

    reply_text = "–ß–∏–º —è —â–µ –º–æ–∂—É –±—É—Ç–∏ –∫–æ—Ä–∏—Å–Ω–∏–º?";
    bot.sendMessage(chat_id, reply_text);
  } else {
    reply_text = "–ú–µ–Ω—ñ –Ω–µ –≤–¥–∞–ª–æ—Å—è —Å—Ç–≤–æ—Ä–∏—Ç–∏ –≥—Ä–∞—Ñ—ñ–∫ üòî\n";
    reply_text += "–û–±–µ—Ä—ñ—Ç—å, –±—É–¥—å –ª–∞—Å–∫–∞, —ñ–Ω—à–∏–π –∑–≤—ñ—Ç:";
    bot.sendMessage(chat_id, reply_text);
  }

  http.end();
}


void sendFallbackMessage(String chat_id) {
  String reply_text = "–Ø –Ω–µ –∑—Ä–æ–∑—É–º—ñ–≤ –≤–∞—à –∑–∞–ø–∏—Ç üòî\n";
  reply_text += "–û–±–µ—Ä—ñ—Ç—å, –±—É–¥—å –ª–∞—Å–∫–∞, –æ–¥–∏–Ω –∑ –ø—É–Ω–∫—Ç—ñ–≤ –º–µ–Ω—é –Ω–∏–∂—á–µ:";
  bot.sendMessageWithReplyKeyboard(chat_id, reply_text, "", getKeyboardByMenu(), true);
}


void handleReportMenu(String chat_id, String text) {
  String reply_text;

  if (text == "–¢–µ–º–ø–µ—Ä–∞—Ç—É—Ä–∞") {
    getPlotByType(chat_id, "temperature");
  } else if (text == "–í–æ–ª–æ–≥—ñ—Å—Ç—å") {
    getPlotByType(chat_id, "humidity");
  } else {
    sendFallbackMessage(chat_id);
  }
}


void handleMainMenu(String chat_id, String text) {
  String reply_text;
  
  if (text == "–ü–æ—Ç–æ—á–Ω—ñ –ø–æ–∫–∞–∑–Ω–∏–∫–∏") {
    float temperature = round(htu.readTemperature() * 10) / 10;
    float humidity = round(htu.readHumidity() * 10) / 10;

    reply_text = "–û—Å—å –ø–æ—Ç–æ—á–Ω—ñ –ø–æ–∫–∞–∑–Ω–∏–∫–∏ –≤ –ø—Ä–∏–º—ñ—â–µ–Ω—ñ:\n";
    reply_text += "üå° –¢–µ–º–ø–µ—Ä–∞—Ç—É—Ä–∞: " + String(temperature) + "¬∞C\n";
    reply_text += "üíß –í–æ–ª–æ–≥—ñ—Å—Ç—å: " + String(humidity) + "%";
    bot.sendMessage(chat_id, reply_text);

    reply_text = "–Ø–∫ —è –º–æ–∂—É –¥–æ–ø–æ–º–æ–≥—Ç–∏ –≤–∞–º —â–µ?";
    bot.sendMessage(chat_id, reply_text);
  } else if (text == "–ó–≤—ñ—Ç –∑–∞ —Ç–∏–∂–¥–µ–Ω—å") {
    menu_state.push(REPORT_MENU);

    reply_text = "–ó–≤—ñ—Ç —Ñ–æ—Ä–º—É—î—Ç—å—Å—è –Ω–∞ –æ—Å–Ω–æ–≤—ñ –∑–∞–ø–∏—Å—ñ–≤ –∑–∞ –æ—Å—Ç–∞–Ω–Ω—ñ–π —Ç–∏–∂–¥–µ–Ω—å.\n";
    reply_text += "–ë—É–¥—å –ª–∞—Å–∫–∞, –æ–±–µ—Ä—ñ—Ç—å, —è–∫–∏–π –∑–≤—ñ—Ç –≤–∏ –±–∞–∂–∞—î—Ç–µ –ø–µ—Ä–µ–≥–ª—è–Ω—É—Ç–∏:";
    bot.sendMessageWithReplyKeyboard(chat_id, reply_text, "", report_menu_keyboard, true);
  } else {
    sendFallbackMessage(chat_id);
  }
}


void sendResponseMenu(String chat_id, MenuState menu) {
  String reply_text;

  switch (menu) {
    case MAIN_MENU:
      reply_text = "–Ø–∫ —è –º–æ–∂—É –¥–æ–ø–æ–º–æ–≥—Ç–∏ –≤–∞–º —â–µ?";
      bot.sendMessageWithReplyKeyboard(chat_id, reply_text, "", main_menu_keyboard, true);
      break;
  }
}


void handleBackCommand(String chat_id) {
  String reply_text;

  if (menu_state.top() == MAIN_MENU) {
    reply_text = "–Ø–∫ —è –º–æ–∂—É –¥–æ–ø–æ–º–æ–≥—Ç–∏ –≤–∞–º —â–µ?";
    bot.sendMessage(chat_id, reply_text);
  } else {
    menu_state.pop();
    sendResponseMenu(chat_id, menu_state.top());
  }
}


void handleStartCommand(String chat_id) {
  String reply_text;

  if (menu_state.empty()) {
    menu_state.push(MAIN_MENU);

    reply_text = "–ü—Ä–∏–≤—ñ—Ç! –Ø –≤–∞—à –ø–æ–º—ñ—á–Ω–∏–∫ –¥–ª—è –∫–æ–Ω—Ç—Ä–æ–ª—é –º—ñ–∫—Ä–æ–∫–ª—ñ–º–∞—Ç—É üè†üå°\n";
    reply_text += "–ß–∏–º —è –º–æ–∂—É –≤–∞—à –¥–æ–ø–æ–º–æ–≥—Ç–∏?";
    bot.sendMessageWithReplyKeyboard(chat_id, reply_text, "", main_menu_keyboard, true);
  } else {
    while (!menu_state.empty()) {
      menu_state.pop();
    }

    menu_state.push(MAIN_MENU);

    reply_text = "–•–º, —è –±—É–≤ –≤–ø–µ–≤–Ω–µ–Ω–∏–π, —â–æ –º–∏ –≤–∂–µ —Ä–æ–∑–º–æ–≤–ª—è–ª–∏ ü§î\n";
    reply_text += "–ß–∏–º —è –º–æ–∂—É –¥–æ–ø–æ–º–æ–≥—Ç–∏?";
    bot.sendMessageWithReplyKeyboard(chat_id, reply_text, "", main_menu_keyboard, true);
  }
}


void handleNewMessages(int num_new_messages) {
  for (int i = 0; i < num_new_messages; i++) {
    String chat_id = String(bot.messages[i].chat_id);
    String text = bot.messages[i].text;

    if (text == "/start") {
      handleStartCommand(chat_id);
      break;
    } else if (text == "–ù–∞–∑–∞–¥") {
      handleBackCommand(chat_id);
      break;
    }

    switch (menu_state.top()) {
      case MAIN_MENU:
        handleMainMenu(chat_id, text);
        break;
      case REPORT_MENU:
        handleReportMenu(chat_id, text);
        break;
    }
  }
}


void setup() {
  Serial.begin(115200);

  Serial.println("Connecting to Wi-Fi.");
  WiFi.begin(WIFI_SSID, WIFI_PASSWORD);
  while (WiFi.status() != WL_CONNECTED) {
    Serial.print(".");
    delay(1000);
  }

  client.setCACert(TELEGRAM_CERTIFICATE_ROOT);
  configTime(0, 0, "pool.ntp.org");

  if (!htu.begin()) {
    while(1);
  }
}


void loop() {
  unsigned long current_millis = millis();

  if (current_millis > last_time_bot_accessed + bot_request_delay) {
    int num_new_messages = bot.getUpdates(bot.last_message_received + 1);

    while (num_new_messages) {
      handleNewMessages(num_new_messages);
      num_new_messages = bot.getUpdates(bot.last_message_received + 1);
    }

    last_time_bot_accessed = current_millis;
  }

  if (current_millis - previous_millis >= readings_interval) {
    previous_millis = current_millis;

    float temperature = round(htu.readTemperature() * 10) / 10;
    float humidity = round(htu.readHumidity() * 10) / 10;
    uint32_t timestamp = getCurrentTime();

    sensor_data[readings_count].temperature = temperature;
    sensor_data[readings_count].humidity = humidity;
    sensor_data[readings_count].timestamp = timestamp;

    readings_count++;

    if (readings_count >= max_readings_count) {
      sendDataToServer();
    }
  }
}
